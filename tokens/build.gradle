plugins {
    id 'java'
    id 'maven-publish'
    id 'com.jfrog.bintray' version '1.8.5'
}

java {
    sourceCompatibility = JavaVersion.VERSION_11
    targetCompatibility = JavaVersion.VERSION_11
}

dependencies {
    compileOnly 'foundation.icon:javaee-api:0.8.6'

    testImplementation project(':testsvc')
    testImplementation 'org.mockito:mockito-core:3.3.3'
    testImplementation 'org.junit.jupiter:junit-jupiter-api:5.6.0'
    testRuntimeOnly 'org.junit.jupiter:junit-jupiter-engine:5.6.0'
}

compileJava {
    // need to add this option to retrieve formal parameter names
    options.compilerArgs += ['-parameters']
}

test {
    useJUnitPlatform()
}

task sourcesJar(type: Jar, dependsOn: classes) {
    classifier 'sources'
    from sourceSets.main.allSource
}

task javadocJar(type: Jar, dependsOn: javadoc) {
    classifier 'javadoc'
    from javadoc.destinationDir
}

def projectName = 'javaee-tokens'
def repoUrl = 'https://github.com/sink772/javaee-tokens'
def snapshotSuffix = rootProject.hasProperty('release') ? '' : '-SNAPSHOT'
version = VERSION + snapshotSuffix

def pomConfig = {
    licenses {
        license {
            name "The Apache Software License, Version 2.0"
            url "http://www.apache.org/licenses/LICENSE-2.0.txt"
            distribution "repo"
        }
    }
    developers {
        developer {
            id "sink772"
            name "Jaechang Namgoong"
            email "sink772@gmail.com"
        }
    }
    scm {
        url repoUrl
    }
}

publishing {
    repositories {
        maven {
            def releasesUrl = "http://ci.arch.iconloop.com/nexus/repository/maven-releases"
            def snapshotsUrl = "http://ci.arch.iconloop.com/nexus/repository/maven-snapshots"
            url = version.endsWith('SNAPSHOT') ? snapshotsUrl : releasesUrl
            credentials {
                username = rootProject.hasProperty('nexusUsername') ? "$nexusUsername" : ''
                password = rootProject.hasProperty('nexusPassword') ? "$nexusPassword" : ''
            }
        }
    }
    publications {
        mavenJava(MavenPublication) {
            groupId = GROUP
            artifactId = projectName
            from components.java
            artifact sourcesJar
            artifact javadocJar
            pom.withXml {
                def root = asNode()
                root.appendNode('name', projectName)
                root.appendNode('description', 'A Java SCORE Library for Standard Tokens')
                root.appendNode('url', repoUrl)
                root.children().last() + pomConfig
            }
        }
    }
}

bintray {
    user = rootProject.hasProperty('bintrayUser') ? "$bintrayUser" : ''
    key = rootProject.hasProperty('bintrayKey') ? "$bintrayKey" : ''
    publications = ['mavenJava']
    publish = true
    pkg {
        repo = 'maven'
        name = projectName
        licenses = ['Apache-2.0']
        vcsUrl = 'https://github.com/sink772/javaee-tokens.git'
        version {
            name = project.version
            gpg {
                sign = true
            }
        }
    }
}
